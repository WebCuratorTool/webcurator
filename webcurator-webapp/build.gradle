import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.springframework.boot' version '2.5.12'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo1.maven.org/maven2'
    }
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
    
    maven{
        // For  com.cloudera.cdh:hadoop-root:0.20.2-cdh3u4
        url = 'https://repository.cloudera.com/artifactory/libs-release-local/'
    }

    maven {
        url = 'http://www.datanucleus.org/downloads/maven2/'
    }
}

group = 'org.webcurator'
version = '3.3.0-SNAPSHOT'
description = 'Web Curator Tool WebApp'

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'

// this needs to be set after the 'java' plugin has been applied
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    publishableArtifacts
}

dependencies {
    implementation('org.netpreserve.openwayback:openwayback-core:2.4.0') {
        exclude group: 'org.netpreserve.commons', module: 'webarchive-commons'
        exclude group: 'com.sleepycat', module: 'je'
    }
    implementation(group: 'org.apache.tiles', name: 'tiles-jsp', version: '3.0.8') {
        // Use Spring boot logging.
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'jcl-over-slf4j'
    }
    implementation 'javax.servlet:jstl:1.2'
    implementation 'commons-fileupload:commons-fileupload:1.4'
    implementation 'commons-dbcp:commons-dbcp:1.4'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'javax.validation:validation-api'

    implementation 'com.h2database:h2:1.4.199'
//    implementation 'mysql:mysql-connector-java:5.1.6'
    implementation 'mysql:mysql-connector-java:8.0.21'
    implementation 'oracle:ojdbc6:11.2.0.3'
    implementation 'org.postgresql:postgresql:42.2.19'

    implementation 'commons-pool:commons-pool:1.3.1-heritrix-1.14.2-webcuratortool-2.0.1'
    implementation(group: 'org.webcurator', name: 'webcurator-core', version: '3.3.+') {
        exclude group: 'joda-time', module: 'joda-time'
        exclude group: 'com.sleepycat', module: 'je'
        exclude group: 'org.archive', module: 'archive-commons'
        exclude group: 'org.apache.hadoop', module: 'hadoop-core'
    }
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.0.0'

    implementation 'org.webjars:webjars-locator:0.30'
    implementation 'org.webcurator:webcurator-ui:3.3.+'

    // NOTE: This is a workaround so that gradle brings in the tests jar.
    testImplementation(group: 'org.webcurator', name: 'webcurator-core', version: '3.3.+', classifier: 'tests') {
        // Use Spring boot logging.
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        //exclude group: 'com.sleepycat', module: 'je'
    }
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'junit:junit:4.12'
    implementation 'org.apache.directory.server:apacheds-server-jndi:1.5.5'
//    implementation 'org.archive.heritrix:heritrix-engine:3.4.0-SNAPSHOT'

    providedCompile 'org.apache.taglibs:taglibs-standard-impl:1.2.5'
//    providedCompile 'org.apache.tomcat.embed:tomcat-embed-jasper:9.0.21'
    providedCompile 'org.apache.tomcat.embed:tomcat-embed-jasper:9.0.60'

}

// just an alias
task install(dependsOn: ['bootWar', 'publishToMavenLocal'])

war {
    includeEmptyDirs = false
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from(sourceSets.main.allJava)
}


publishing {
    publications {
        webApp(MavenPublication) {
            artifact bootWar
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

build.dependsOn war, sourcesJar


// update the version number in application.properties
processResources {
    filesMatching('**/application.properties') {
        filter ReplaceTokens, tokens: [
                "project.version": "${version}".toString()
        ]
    }
}



